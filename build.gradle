plugins {
	id 'groovy'
	id 'org.jetbrains.intellij' version '1.17.4'
	id 'com.diffplug.spotless' version '6.25.0'
}

intellij {
	version = '2024.3'
	plugins = ['java']
}


ext.pitVersion = '1.20.0'
ext.pitJunit5PluginVersion = '1.2.3'

sourceCompatibility = 17
targetCompatibility = 17


// workaround for http://issues.gradle.org/browse/GRADLE-2538
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'

repositories {
	mavenCentral()
	mavenLocal()
}


dependencies {
	implementation("org.pitest:pitest-command-line:$pitVersion") { transitive = false }
	implementation("org.pitest:pitest-entry:$pitVersion") { transitive = false }
	implementation("org.pitest:pitest:$pitVersion") { transitive = false }
	implementation "org.pitest:pitest-junit5-plugin:$pitJunit5PluginVersion"
	implementation 'org.apache.commons:commons-text:1.10.0'
	implementation('org.junit.platform:junit-platform-launcher:1.9.2') { transitive = false }
	compileOnly 'org.codehaus.groovy:groovy-all:3.0.19'
	testCompileOnly 'org.codehaus.groovy:groovy-all:3.0.19'
	testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
		exclude group: 'org.codehaus.groovy'
	}
}

jar {
	metaInf {
		from('META-INF') {
			include 'plugin.xml'
			include 'pluginIcon.svg'
		}
	}
}

wrapper {
	gradleVersion "8.6"
}

spotless {
	java {
		palantirJavaFormat()
	}
	groovy {
		greclipse()
		excludeJava()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}