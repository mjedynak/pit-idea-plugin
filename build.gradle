apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: "jacoco"
apply plugin: 'idea'
apply from: 'idea.gradle'

// --- properties ---
ext.ideaInstallationPath = 'C:/Programs/ideaIU'
ext.ideaJdk = 'IntelliJ IDEA IU-182.4129.33'
ext.pitVersion = '1.4.2'
sourceCompatibility = 1.8
// --- properties ---

// workaround for http://issues.gradle.org/browse/GRADLE-2538
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

build.dependsOn jacocoTestReport

repositories {
    maven {
        name = "nexus.ucm.be"
        url = "http://nexus.ucm.be/nexus/repository/maven-sec"
    }
    mavenCentral()
    mavenLocal()
}

configurations {
    releaseJars
}

task dist(type: Zip) {
    from(jar.outputs.files) {
        into('pit-idea-plugin/lib')
    }
    from(configurations.releaseJars) {
        into('pit-idea-plugin/lib')
    }
    archiveName 'pit-idea-plugin.zip'
    dependsOn build
}

dependencies {

    releaseJars("org.pitest:pitest-command-line:$pitVersion") { transitive = false }
    releaseJars("org.pitest:pitest-entry:$pitVersion") { transitive = false }
    releaseJars("org.pitest:pitest:$pitVersion") { transitive = false }
    releaseJars("com.thoughtworks.xstream:xstream:1.4.8")

    compile fileTree(dir: ideaInstallationPath + '/lib', include: '*.jar')
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
    testCompile 'org.objenesis:objenesis:1.2'
}

jar {
    metaInf {
        from('/META-INF') {
            include 'plugin.xml'
        }
    }
}