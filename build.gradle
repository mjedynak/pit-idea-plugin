apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: "jacoco"
apply plugin: 'idea'
apply from: 'idea.gradle'

// --- properties ---
ext.ideaInstallationPath = 'C:/Program Files (x86)/JetBrains/IntelliJ IDEA Community Edition 14.0'
ext.ideaJdk = 'IDEA IC-135.475'
ext.pitVersion = '1.1.4'
sourceCompatibility = 1.6
// --- properties ---

// workaround for http://issues.gradle.org/browse/GRADLE-2538
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'

jacoco {
    toolVersion = "0.7.0.201403182114"
}

jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

build.dependsOn jacocoTestReport

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    releaseJars
}

task dist(type: Zip) {
    from(jar.outputs.files) {
        into('pit-idea-plugin/lib')
    }
    from(configurations.releaseJars) {
        into('pit-idea-plugin/lib')
    }
    archiveName 'pit-idea-plugin.zip'
    dependsOn build
}

dependencies {

    releaseJars("org.pitest:pitest-command-line:$pitVersion") { transitive = false }
    releaseJars("org.pitest:pitest:$pitVersion") { transitive = false }

    compile fileTree(dir: ideaInstallationPath + '/lib', include: '*.jar')
    compile 'org.codehaus.groovy:groovy-all:2.2.1'
    testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'org.objenesis:objenesis:1.2'
}

jar {
    metaInf {
        from('/META-INF') {
            include 'plugin.xml'
        }
    }
}