plugins {
    id 'groovy'
    id 'org.jetbrains.intellij' version '1.13.3'
}

intellij {
    version = '2023.3'
    plugins = ['java']
}

// --- properties ---
ext.pitVersion = '1.15.8'
ext.pitJunit5PluginVersion = '1.2.1'
sourceCompatibility = 17
targetCompatibility = 17
// --- properties ---

// workaround for http://issues.gradle.org/browse/GRADLE-2538
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'

repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    implementation("org.pitest:pitest-command-line:$pitVersion") { transitive = false }
    implementation("org.pitest:pitest-entry:$pitVersion") { transitive = false }
    implementation("org.pitest:pitest:$pitVersion") { transitive = false }
    implementation "org.pitest:pitest-junit5-plugin:$pitJunit5PluginVersion"
    implementation 'org.apache.commons:commons-text:1.10.0'
    compileOnly 'org.codehaus.groovy:groovy-all:3.0.19'
    testCompileOnly 'org.codehaus.groovy:groovy-all:3.0.19'
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

jar {
    metaInf {
        from('META-INF') {
            include 'plugin.xml'
            include 'pluginIcon.svg'
        }
    }
}

wrapper {
    gradleVersion "8.0.2"
}